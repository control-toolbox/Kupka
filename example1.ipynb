{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Example 1\n",
    "\n",
    "## Problem\n",
    "\n",
    "$$(OCP)_1\\left\\{\\begin{array}{l}\n",
    "Min \\int_0^{t_f}x\\sqrt{1+u^2}dt\\\\\n",
    "\\dot{x} = u\\\\\n",
    "u\\in\\R\n",
    "\\end{array}\n",
    "\\right.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "Pkg.activate(\".\")\n",
    "#\n",
    "using OptimalControl\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using DifferentialEquations\n",
    "using Roots     # solve an equation f(x)=0 where f is from R to R\n",
    "using LaTeXStrings\n",
    "using Plots\n",
    "using Test"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Control-toolbox definition of the problem"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "t0 = 0\n",
    "tf = 2\n",
    "x0 = 1\n",
    "xf = 1\n",
    "@def ocp begin\n",
    "    t ∈ [ t0, tf ], time\n",
    "    x ∈ R, state\n",
    "    u ∈ R, control\n",
    "    x(t0) == x0\n",
    "    x(tf) == xf\n",
    "    ẋ(t) ==  u(t)\n",
    "    ∫(x(t)*(1 + u(t)^2)^(1/2)) → min\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Hamiltonian flow\n",
    "The Hamiltonian is\n",
    "$$H(x,u,p) =  -x\\sqrt{1+u^2} + up,$$\n",
    "and for $|p| < |x|$ the maximization of the Hamiltonian have the solution\n",
    "$$u(x,p) = sign(x)p\\sqrt{1. /(x^2-p^2)}.$$\n",
    "Then the true Hamiltonian is $H_r(x,p)=H(x,u(x,p),p)$. \n",
    "\n",
    "We note $z(t) = (x(t),p(t))$, then the hamiltonian flow is the function\n",
    "$\\phi(.,z_0) = \\phi(.,x_0,p_0)$ solution of the initial value problem\n",
    "$$(IVP)\\left\\{\n",
    "\\begin{array}{l}\n",
    "\\dot{z} = \\vec{H}(z) = \\begin{pmatrix}\n",
    "\\dot{x}\\\\ \\dot{p}\n",
    "\\end{pmatrix} =\n",
    "\\begin{pmatrix}\n",
    "\\dfrac{\\partial H(z)}{\\partial p}\\\\\n",
    "-\\dfrac{\\partial H(z)}{\\partial x}\n",
    "\\end{pmatrix}\\\\\n",
    "z(0) = z_0 = (x_0,p_0).\n",
    "\\end{array}\n",
    "    \\right.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Compute the  Flow\n",
    "# contol function\n",
    "u(x, p) = sign(x[1])*p[1]*sqrt(1. /(x[1]^2-p[1]^2))  \n",
    "\n",
    "# the Flow function computes the hamiltonian flow\n",
    "ocp_flow = Flow(ocp, u)\n",
    "nothing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "# ocp_flow.ode_sol is the result of the solve function from the DifferentailEquations package\n",
    "\n",
    "# Compute flows for different value of the initial costate $p_0\n",
    "Int_p0 = -0.985:0.25:0.98   # intervalle of p_0\n",
    "Δt = (tf - t0)/100          # \n",
    "plt_x = plot()          # plot of the state x(t)\n",
    "plt_p = plot()          # plot of the costate p(t)\n",
    "plt_u = plot()          # plot of the control u(t)\n",
    "plt_phase = plot()      # plot (x,p)\n",
    "my_color = :blue\n",
    "for p0 in Int_p0        # plot for each p_0 in Int_p0 \n",
    "    flow_p0 = ocp_flow((t0, tf), x0, p0, reltol = 1e-8, abstol = 1e-8, saveat = Δt)\n",
    "    T = flow_p0.ode_sol.t\n",
    "    X = flow_p0.ode_sol[1,:]\n",
    "    P = flow_p0.ode_sol[2,:]\n",
    "    par = atanh(p0./x0)\n",
    "    plot!(plt_x,T,X,color = my_color)\n",
    "    plot!(plt_p,T,P,color = my_color)\n",
    "    plot!(plt_u,T,u.(X,P),color = my_color)  \n",
    "    plot!(plt_phase,X,P,color = my_color)      \n",
    "end\n",
    "\n",
    "# Plots\n",
    "plot!(plt_x,xlabel = L\"t\",ylabel=L\"x(t,p_0)\",legend=false, ylims=(0.,5.))\n",
    "plot!(plt_p,xlabel = L\"t\",ylabel=L\"p(t,p_0)\",legend=false, ylims=(-1.5,5.))\n",
    "plot!(plt_u,xlabel = L\"t\",ylabel=L\"u(t,p_0)\",legend=false, ylims=(-6.,5.))\n",
    "plot!(plt_phase,xlabel = L\"x(t,p_0)\",ylabel=L\"p(t,p_0)\",legend=false, xlims=(0.,2.), ylims=(-1.,5.))\n",
    "\n",
    "fig1_example1 = plot(plt_x,plt_p,plt_u,plt_phase,layout = (2,2))\n",
    "Plots.pdf(fig1_example1, \"./fig-example1/fig1_example1.pdf\")\n",
    "plot(fig1_example1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Conjugate points\n",
    "The time $\\tau$ is said to be conjugate to the the time $t_0=0$ if the solution of the Jacobi equation\n",
    "$$\n",
    "\\dot{\\delta z}(t) = \\dfrac{\\partial \\vec{H}}{\\partial z}(z(t,z_0))\\delta z(t)\n",
    "$$\n",
    "with the initial condition\n",
    "$\\delta z(0) = \\begin{pmatrix}0\\\\1\n",
    "\\end{pmatrix}$, is vertical at time $\\tau$, that is $\\delta x(\\tau) = 0$.\n",
    "\n",
    "We first compute by automatic differentiation the flow of the Jacobi equation with the initial condition \n",
    "$$\\delta z(t,x_0,p_0)=\\dfrac{\\partial z}{\\partial p_0}z(t,x_0,p_0)$$ \n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#\n",
    "# Conjugate points\n",
    "\n",
    "\"\"\"\n",
    "    Compute the flow of the Jacobi equation for the initial condition δz(0) = (0,1)\n",
    "    sol = flow_jacobi(T,x0,p0)\n",
    "    input : \n",
    "    T : time where we want δz(t) \n",
    "        list or array (t0,t1,...,tN)\n",
    "    x0 : initial state\n",
    "         Real(n), here a real\n",
    "    p0 : initial costate\n",
    "         Real(n), here a real\n",
    "    output : \n",
    "    sol : (δz(t0),δz(t1),...,δz(tN))\n",
    "          Vector of vector of dimension 2n, the type of the element are the same as the type of p0 because we use after the automatic differentiation.\n",
    "          To modifie if n > 1\n",
    "\n",
    "\"\"\"\n",
    "function flow_jacobi(tspan,x0,p0)\n",
    "    #n = length(x0)\n",
    "\n",
    "    #nb_t = length(T)\n",
    "    # type because we use ForwardDiff\n",
    "    #sol = [Vector{typeof(p0[1])}(undef,2n) for _ in 1:nb_t] # Vector -> Matrix if n >  1\n",
    "                                                         # typeof(p0) -> typeof(p0[1]), type of the element of p0\n",
    "\n",
    "        # flow_p0 compute de flow at t oh the hamiltonian system\n",
    "        flow_p0(p_0) = ocp_flow(tspan, x0, p_0, reltol = 1e-10, abstol = 1e-10).ode_sol.u[end]\n",
    "        # temp is the solution at t of the derivative with respect to p0 of the flow\n",
    "        sol = ForwardDiff.derivative(flow_p0, p0)  # p0 is a real\n",
    "        #sol[i] = det(temp[1:n,1:n])\n",
    "    return sol\n",
    "end\n",
    "\n",
    "p0 = -0.985\n",
    "t0tf = (0., 2.)\n",
    "\n",
    "Int_t0tf = range(t0,stop=tf,length=100)\n",
    "n_t = length(Int_t0tf)\n",
    "Sol = zeros(n_t,2)\n",
    "for i in 1:n_t\n",
    "    Sol[i,:] = flow_jacobi((t0,Int_t0tf[i]),x0,p0)\n",
    "end\n",
    "plt_conj1 = plot()\n",
    "plot!(plt_conj1,Int_t0tf,Sol[:,1])  # as n=1 the det is the number\n",
    "plot!(plt_conj1,xlabel = L\"t\",ylabel=L\"\\delta x(t,p_0)\",legend=false, ylims=(-10.,10.))\n",
    "\n",
    "Plots.pdf(plt_conj1, \"./fig-example1/fig2_example1.pdf\")\n",
    "plot(plt_conj1)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Then we numerically compute the conjugate point by solving $\\delta x(t)=\\delta z(t,x_0,p_0)_1=0$, for $x_0=1.$ and $p_0=0.985$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# compute the fisrt conjugate point\n",
    "function F(t0,tau,x0,p0)\n",
    "    tspan = (t0,tau)\n",
    "#    return [flow_jacobi(tspan,x₀,p0)(τ)[1]]\n",
    "    return flow_jacobi(tspan,x0,p0)\n",
    "end\n",
    "\n",
    "# compute τ0\n",
    "delta_x(tau) = F(t0,tau,x0,p0)[1]\n",
    "\n",
    "#sol = nlsolve(δx,[0.5])\n",
    "using Roots\n",
    "#τ0 = sol.zero\n",
    "tau0 = find_zero(delta_x, (0.4, 0.6))\n",
    "println(\"For p0 = \", p0, \" tau_0 = \", tau0)\n",
    "\n",
    "plot!(plt_conj1,[tau0],[flow_jacobi((t0,tau0),x0,p0)],seriestype=:scatter)\n",
    "Plots.pdf(plt_conj1, \"./fig-example1/fig3_example1.pdf\")\n",
    "plot(plt_conj1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Compute the conjugate loci\n",
    "We compute conjugate loci by path following algorithm\n",
    "\n",
    "We define $F(\\tau,p_0) = \\delta x(\\tau,p_0)$ and we suppose that \n",
    "$\\dfrac{\\partial F}{\\partial\\tau}(\\tau,p_0)$ is  inversible, then by the implicit function theorem the conjugate time is a function of $p_0$. So, as here $p_0\\in\\R$, we can compute them by solving the initial value problem\n",
    "$$(IVP_{conj.points})\\left\\{\n",
    "\\begin{array}{l}\n",
    "\\dot{\\tau} = -\\dfrac{\\partial F}{\\partial\\tau}(\\tau,p_0)^{-1}\\dfrac{\\partial F}{\\partial p_0}(\\tau,p_0)\\\\\n",
    "\\tau(p_0) = \\tau_0.\n",
    "\\end{array}\n",
    "    \\right.$$\n",
    "\n",
    "### Remark\n",
    "The derivative $\\dfrac{\\partial F}{\\partial\\tau}(\\tau,p_0) = \\dfrac{\\partial \\delta x}{\\partial\\tau}(\\tau,p_0)$ is equal to  the first component of the second member of the Jacobi equation\n",
    "$\\dfrac{\\partial \\vec{H}}{\\partial z}(z(t,z_0))\\delta z(t)$.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# right hand side of the hamiltonian flow\n",
    "Hvec! = ocp_flow.rhs!\n",
    "\n",
    "#conjugates points by path following\n",
    "\"\"\"\n",
    "    Compute the right hand side of the conj.points IVP equation\n",
    "    tau_point = rhs_path(tau , par, p0)\n",
    "    For the structure of the rhs_path see DifferentialEquaions package\n",
    "\"\"\"\n",
    "function rhs_path(tau , par, p0)\n",
    "    n = length(p0)\n",
    "    tau1 = tau[1]     # tau is a vector\n",
    "    z0 = [x0,p0]\n",
    "    zpoint = similar(z0)\n",
    "    Hvec!(zpoint,z0) = ocp_flow.rhs!(zpoint,z0,par,tau1)\n",
    "    # zpoint is the second member of the Hamiltonian flow\n",
    "    Hvec!(zpoint,z0)     \n",
    "    dHvec = Matrix{typeof(p0[1])}(undef,2n,2n) #zeros(2*n,2*n)\n",
    "    # compute z(tau)\n",
    "    z = ocp_flow((t0, tau1), x0, p0, reltol = 1e-8, abstol = 1e-8).ode_sol(tau1) \n",
    "  \n",
    "    #derivative w.r.t. τ\n",
    "    ForwardDiff.jacobian!(dHvec,Hvec!,zpoint,z) \n",
    "    println(\"t0 = \", t0)\n",
    "    println(\"tau1 = \", tau1)\n",
    "    delta_z = flow_jacobi((t0,tau1),x0,p0)\n",
    "    dd_tau = (dHvec*delta_z)[1]    \n",
    "\n",
    "    #derivative w.r.t. p0  \n",
    "    Ftau(p0) = F(t0,tau1,x0,p0)[1] \n",
    "    # delta_x(tau)\n",
    "    dd_p0 = ForwardDiff.derivative(Ftau, p0) \n",
    "    return [-(1/dd_tau)*dd_p0]\n",
    "end\n",
    "\n",
    "p0=-0.985\n",
    "tau = 0.5\n",
    "x0 = 1.\n",
    "rhs_jacobi = rhs_path(0.5 , 1., p0)\n",
    "println(\"x0 = \", x0, \", p0 = \", p0)\n",
    "\n",
    "include(\"./test/example1-analytique.jl\")\n",
    "\n",
    "println(rhs_path(0.5 , 1., p0))\n",
    "println(rhs_path_ana(0.5 , 1., p0))\n",
    "@test isapprox(rhs_path(0.5 , 1., p0),rhs_path_ana(0.5 , 1., p0); rtol = 1.e-7)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "   Compute the conjugate times by path by integration of the (IVP_conj.points) problem\n",
    "\"\"\"\n",
    "function conj_point(p0span, tau0)\n",
    "    #\n",
    "    pb = ODEProblem(rhs_path,[tau0],p0span,[1.])\n",
    "    sol = DifferentialEquations.solve(pb, reltol = 1e-8, abstol = 1e-8)\n",
    "    return sol\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "# conjugate point\n",
    "\n",
    "println(\"p0 = \", p0)\n",
    "println(tau0)\n",
    "\n",
    "p0span = (p0, -0.5)\n",
    "sol = conj_point(p0span,tau0)\n",
    "plt_conj_point = plot(sol,xlabel = L\"p_0\", ylabel = L\"\\tau\",color=my_color)\n",
    "TT = sol.u              # conjugate times\n",
    "nb_t = length(sol.t)\n",
    "T = zeros(nb_t)\n",
    "X = zeros(nb_t)\n",
    "for i in 1:nb_t\n",
    "    T[i] = TT[i][1]      # τ\n",
    "                         # sol.t[i] = p0\n",
    "    X[i] = ocp_flow((t0, T[i][1]), x0, sol.t[i], reltol = 1e-8, abstol = 1e-8, saveat = Δt).ode_sol[1,end] # state at the conjugate time \n",
    "end\n",
    "\n",
    "plot!(plt_x,T,X,linewidth=3,color = :red, legend=false, xlims=(0.,2.0), ylims=(0.,5.))\n",
    "plt_conj2 = plot(plt_x,plt_conj_point,layout = (2,1),legend=false,size=(600,600))\n",
    "Plots.pdf(plt_conj2, \"./fig-example1/fig4_example1.pdf\")\n",
    "plot(plt_conj2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
